plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
  id "org.jetbrains.kotlin.jvm" version "${version_kotlin}"
}

version = project.mod_version
group = "${project.mod_group}.${project.mod_id}"

base {
	archivesName = project.mod_id
}

repositories {
	mavenLocal()

	// Cardinal Components API
	maven { url = "https://maven.ladysnake.org/releases" }

	// Valkyrien Skies
	maven { url = "https://maven.valkyrienskies.org" }

	// Create
	maven { url = "https://mvn.devos.one/snapshots/" }
	maven { url = "https://cursemaven.com" } // Forge Config API Port
	maven { url = "https://maven.tterrag.com/" } // Flywheel
	maven { url = "https://maven.jamieswhiteshirt.com/libs-release" } // Reach Entity Attributes

	// Create, Lodestone Lib
	maven { url = "https://jitpack.io/" } // Mixin Extras, Fabric ASM

	// Create: Interactive, Midnight Lib
	maven { url = "https://api.modrinth.com/maven" }

}

dependencies {
	
// Loader
	minecraft("com.mojang:minecraft:${project.version_minecraft}")
	mappings(loom.layered {
		it.mappings("net.fabricmc:yarn:${project.version_minecraft}+build.${project.version_yarn}:v2")
		it.mappings file("mappings/create-conflict-fix.tiny")
	})
	modImplementation("net.fabricmc:fabric-loader:${project.version_fabric}")
	modImplementation("net.fabricmc:fabric-language-kotlin:${project.version_fabrickotlin}+kotlin.${project.version_kotlin}")

	// Fabric API
	modImplementation("net.fabricmc.fabric-api:fabric-api:${project.version_fabricapi}+${project.version_minecraft}")

	// Cardinal Components API
	modImplementation("dev.onyxstudios.cardinal-components-api:cardinal-components-chunk:${project.version_cardinalcomponentsapi}")
	include("dev.onyxstudios.cardinal-components-api:chunk:${project.version_cardinalcomponentsapi}")

	// Valkyrien Skies
	modImplementation("org.valkyrienskies:valkyrienskies-120-fabric:${project.version_valkyrienskies}") {
		exclude group: "maven.modrinth", module: "sodium"
		exclude group: "maven.modrinth", module: "modmenu"
		transitive = false
	}
	modCompileOnly("org.valkyrienskies.core:api:${project.version_valkyrienskies_core}") {transitive = false}
	modCompileOnly("org.valkyrienskies.core:api-game:${project.version_valkyrienskies_core}") {transitive = false}
	modCompileOnly("org.valkyrienskies.core:util:${project.version_valkyrienskies_core}") {transitive = false}
	modCompileOnly("org.valkyrienskies.core:impl:${project.version_valkyrienskies_core}") {transitive = false}

	// Lodestone Lib
	modImplementation("com.github.cph101:Lodestonelib-fabric:v${project.version_lodestonelib}-${project.version_minecraft}") {transitive = false}

	// Create
	modImplementation("com.simibubi.create:create-fabric-${project.version_minecraft}:${project.version_create}+mc${project.version_minecraft}")


	// ModMenu
	modRuntimeOnly("maven.modrinth:modmenu:${project.version_modmenu}")
	// Create: Interactive
	modRuntimeOnly("maven.modrinth:interactive:${project.version_createinteractive}")
	// Midnight Lib
	modRuntimeOnly("maven.modrinth:midnightlib:${project.version_midlightlib}-fabric")
	// Cloth Config
	modRuntimeOnly("maven.modrinth:cloth-config:${project.version_clothconfig}+fabric")

}

loom {
	accessWidenerPath = file("src/main/resources/${project.mod_id}.accesswidener")
}

processResources {
	filesMatching([
			"*.json",
			"data/**/*.json",
			"assets/**/*.json"
	]) {

		expand  "mod_id"      : project.mod_id,
				"mod_group"   : project.mod_group,
				"mod_version" : project.mod_version,
				"mod_name"    : project.mod_name,

				"version_minecraft" : project.version_minecraft,
				"version_fabric"    : project.version_fabric,
				"version_kotlin"    : project.version_kotlin,

				"version_fabricapi" : project.version_fabricapi,

				"version_cardinalcomponentsapi" : project.version_cardinalcomponentsapi

	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
	kotlinOptions {
		jvmTarget = 17
	}
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}